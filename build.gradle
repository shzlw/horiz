plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group = 'com.shzlw'
version = '0.0.1'

repositories {
    jcenter()
}

dependencies {
    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.0'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.957'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.powermock:powermock-mockito-release-full:1.6.4'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'horiz'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Horiz'
                description = 'A Java library that uses S3 as datastore and provides simplified Key-Value APIs'
                url = 'https://github.com/shzlw/horiz'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/shzlw/horiz/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'shzlw'
                        name = 'Zhonglu Wang'
                    }
                }
            }
        }
    }
}
